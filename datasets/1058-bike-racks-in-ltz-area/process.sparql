PREFIX qrowd-vocab: <http://qrowd-project.eu/vocab/>
PREFIX fn: <http://jsa.aksw.org/fn/gml/>

CONSTRUCT {
  ?bikerack
    a qrowd-vocab:BikeRack ;
    qrowd-vocab:street ?street ;
    qrowd-vocab:area ?area ;
    qrowd-vocab:type ?type ;
    qrowd-vocab:capacity ?capacity ;
    qrowd-vocab:label ?label ;
    qrowd-vocab:description ?description ;
    qrowd-vocab:wkt ?wkt ;
    .
}
WHERE {
  # Load the content of the relative URL (interpreted as a reference to a file) as a string into ?src
  <rastrelliere.gml> url:text ?src .

  # Parse the string as XML
  BIND(STRDT(?src, xsd:xml) AS ?xml) .

  # Run an XPath expression which yields a list of nodes. Every node becomes a binding to ?o.
  # test local name
  ?xml xml:unnest ("//*[local-name()='rastrelliere']" ?node)
 
  # Read out attributes from each (xml) node
  BIND(xml:path(?node, "@fid") AS ?id)
  BIND("http://qrowd-project.eu/resource/" AS ?ns)
  BIND(URI(CONCAT(?ns, ?id)) AS ?bikerack)
  BIND(xml:path(?node, "//*[local-name()='via']") AS ?street)
  BIND(xml:path(?node, "//*[local-name()='zona']") AS ?area)
  BIND(xml:path(?node, "//*[local-name()='tipologia']") AS ?type)
  BIND(xml:path(?node, "//*[local-name()='n_posti']") AS ?capacity)
  BIND(xml:path(?node, "//*[local-name()='fumetto']") AS ?label)
  BIND(xml:path(?node, "//*[local-name()='descrizione']") AS ?description)
  ?node xml:unnest ("//*[local-name()='geometryProperty']/*" ?g )
  BIND(fn:gml2Wkt(str(?g)) AS ?wkt)
}
