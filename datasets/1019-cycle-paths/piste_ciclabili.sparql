PREFIX qrowd-resource: <http://qrowd-project.eu/resource/>
PREFIX qrowd-vocab: <http://qrowd-project.eu/vocab/>

# Mappings of types to IRIs
INSERT DATA {
  qrowd-resource:RT1 rdfs:label "Pista di raccordo" .
  qrowd-resource:RT2 rdfs:label "Pista verde" .
}


CONSTRUCT {
  ?s
    a qrowd-vocab:BikeRack ;
    qrowd-vocab:type ?t ;
    rdfs:label ?l ;
    rdfs:comment ?c ;
    .
}
WHERE {
  # Load the content of the relative URL (interpreted as a reference to a file) as a string into ?src
  <piste_ciclabili.gml> url:text ?src .

  # Parse the string as XML
  BIND(STRDT(?src, xsd:xml) AS ?xml) .

  # Run an XPath expression which yields a list of nodes. Every node becomes a binding to ?o.
  ?xml xml:unnest ("//*[local-name()='piste_ciclabili']" ?o)
 
  # Read out attributes from each (xml) node
  BIND(xml:path(?o, "//@fid") AS ?id)

  BIND("http://qrowd-project.eu/resource/" AS ?ns)

  BIND(URI(CONCAT(?ns, ?id)) AS ?s)
  BIND(xml:path(?o, "//*[local-name()='tipo']") AS ?tipo)
  BIND(xml:path(?o, "//*[local-name()='fumetto']") AS ?l)
  BIND(xml:path(?o, "//*[local-name()='descrizione']") AS ?c)
  # TODO Add conversion function for geo reference systems...

  # Check the local store for whether there is an URI for the strings of ?tipo
  OPTIONAL { ?tu rdfs:label ?tipo }

  # If there was an URI, use it, otherwise, generate a new URI
  BIND(COALESCE(?tu, URI(CONCAT(?ns, ENCODE_FOR_URI(?tipo)))) AS ?t)
}

#SELECT DISTINCT ?o {
#  ?s eg:type ?o
#}

