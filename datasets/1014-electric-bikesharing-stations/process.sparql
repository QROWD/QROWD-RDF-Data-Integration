# Static triples; used for ad-hoc ontology

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
CONSTRUCT {
  eg:ElectricBikeSharingDataset
    a dcat:Dataset ;
    dct:identifier "BikeSharing" ;
    dct:title "Bike-Sharing-Stations" ;
    dct:description "Stuff" ;
    dcat:keyword "Keyword", "Keyword" ;
    dcat:distribution eg:resource-electric-bike-sharing ;
    .

  eg:resource-electric-bike-sharing
    a dcat:Distribution ;
#   dct:identifier \"$DISTRIBUTION_NAME\" ;
    dct:title "Bike-Sharing-Stations" ;
    dct:description "RDF dataset with BikeSharing Stations" ;
# Hijacking the dcat namespace to refer to a set of graphs within this dataset
    dcat:accessURL <dataset.nq> ;
    .

} WHERE {
  #Empty graph pattern evaluates to a single solution which does not bind variables, hence the triples above are emitted
}


PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX q: <http://qrowd-project.eu/ontology/>
CONSTRUCT {
  ?s
    a q:BikeStation ;
    q:id ?id ;
    rdfs:label ?name ;
    q:address ?address ;
    q:bikes ?bikes ;
    q:slots ?slots ;
    q:totalSlots ?totalSlots ;
    wgs:long ?x ;
    wgs:lat ?y ;
    .
}
{
  <trento-bike-sharing.json> url:text ?src .
  BIND(STRDT(?src, xsd:json) AS ?json) .

  # TODO the index var ?foo should be optional
  ?json json:unnest (?i ?foo) .

  BIND("http://qrowd-project.eu/resource/" AS ?ns)

  BIND(json:path(?i, "$.id") AS ?id)
  BIND(URI(CONCAT(?ns, ENCODE_FOR_URI(?id))) AS ?s)
  BIND(json:path(?i, "$.name") AS ?name)
  BIND(json:path(?i, "$.address") AS ?address)
  BIND(json:path(?i, "$.bikes") AS ?bikes)
  BIND(json:path(?i, "$.slots") AS ?slots)
  BIND(json:path(?i, "$.totalSlots") AS ?totalSlots)
  BIND(json:path(?i, "$.position[0]") AS ?x)
  BIND(json:path(?i, "$.position[1]") AS ?y)
}

