
# Static triples; used for ad-hoc ontology

PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX qrowd-vocab: <http://qrowd-project.eu/vocab/>
PREFIX qrowd-resource: <http://qrowd-project.eu/resource/>

CONSTRUCT {
  qrowd-resource:meteoStationsDataset
    a dcat:Dataset ;
    dct:identifier "meteo-stations" ;
    dct:title "Meteo-Stations" ;
    dct:description "Meteorological data from an automatic measurement network" ;
    dcat:keyword "meteorology", "temperature", "precipitation", "wind", "wind direction", "radiation" ;
    dcat:distribution qrowd-resource:resource-meteo-stations ;
    .

  qrowd-resource:resource-resource-meteo-stations
    a dcat:Distribution ;
#   dct:identifier \"$DISTRIBUTION_NAME\" ;
    dct:title "Meteo-Stations" ;
    dct:description "RDF dataset with meteorological data for Trento." ;
# Hijacking the dcat namespace to refer to a set of graphs within this dataset
    dcat:accessURL <dataset.nq> ;
    .

  qrowd-vocab:quote
    rdfs:label "I don't know what this quote measures" ;
    .
  
  qrowd-vocab:east
    rdfs:label "I don't know what reference system this refers to" ;
    .

  qrowd-vocab:north
    rdfs:label "I don't know what reference system this refers to" ;
    .
} WHERE {
  #Empty graph pattern evaluates to a single solution which does not bind variables, hence the triples above are emitted
}


PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
CONSTRUCT {
  ?s
    a qrowd-vocab:WeatherStation ;
    rdfs:label ?l ;
    qrowd-vocab:shortName ?sl ;
    qrowd-vocab:code ?c ;
    qrowd-vocab:quote ?q ;
    geo:long ?long ;
    geo:lat ?lat ;
    qrowd-vocab:east ?east ;
    qrowd-vocab:north ?north ;
    qrowd-vocab:start ?start ;
    qrowd-vocab:end ?end ;
    .
}
WHERE {
  <listaStazioni.xml> url:text ?src .
  BIND(STRDT(?src, xsd:xml) AS ?xml)

  ?xml xml:unnest ("//anagrafica" ?o)

  BIND("http://qrowd-project.eu/resource/" AS ?ns)

  # Create subject URI
  BIND(xml:path(?o, "//codice") AS ?c)
  BIND(URI(CONCAT(?ns, ?c)) AS ?s)

  # Attributes
  BIND(xml:path(?o, "//nome") AS ?l)
  BIND(xml:path(?o, "//nomebreve") AS ?sl)
  BIND(xsd:integer(xml:path(?o, "//quota")) AS ?q)
  BIND(xsd:decimal(xml:path(?o, "//longitudine")) AS ?long)
  BIND(xsd:decimal(xml:path(?o, "//latitudine")) AS ?lat)
  BIND(xsd:decimal(xml:path(?o, "//est")) AS ?east)
  BIND(xsd:decimal(xml:path(?o, "//north")) AS ?north)

  # TODO Add a date:parse(format, string) sparql extension
  BIND((xml:path(?o, "//inizio")) AS ?start)
  BIND((xml:path(?o, "//fine")) AS ?end)

}

